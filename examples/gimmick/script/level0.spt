##############################################################################
# LEVEL 0 PUEBLO
##############################################################################
INC_DECORATIONS decorations0.spt

ENTERING GAME

	IF $PUERTA = 0 ## VALOR SI NO LLEGAMOS DESDE NINGUNA PUERTA
	THEN
		SET FLAG $PUERTA = 22
		SET $SCHOOL = #$PROGRESO 
	END

	IF TRUE
	THEN
		SET FLAG $DISPARO = 0
		SET $PROGRESO = #$SCHOOL ## al llegar al pueblo se guarda el progreso que hayamos logrado
	END
	
	## SI TENEMOS LOS 4 FRAGMENTOS VEMOS EL FINAL WENO
	IF $PROCESOR1 = 1
	IF $PROCESOR2 = 1
	IF $PROCESOR3 = 1
	IF $PROCESOR4 = 1
	THEN
		SET $SCHOOL = 30
	END

	IF FLAG $PUERTA = 60
	THEN
		WARP_TO 18, 3, 7
	END
	
	IF FLAG $PUERTA = 7
	THEN
		WARP_TO 18, 11, 7
	END
	
	IF FLAG $PUERTA = 11
	THEN
		WARP_TO 16, 7, 7
	END
	
	IF FLAG $PUERTA = 21
	THEN
		WARP_TO 17, 7, 7
	END
	
	IF FLAG $PUERTA = 30
	THEN
		WARP_TO 14, 7, 8
	END	

	IF FLAG $PUERTA = 41
	THEN
		WARP_TO 4, 10, 8
	END	
	
	IF FLAG $PUERTA = 51
	THEN
		WARP_TO 15, 7, 2
	END	

	IF FLAG $PUERTA = 71
	THEN
		WARP_TO 7, 4, 7
	END
	
	IF FLAG $PUERTA = 79
	THEN
		WARP_TO 15, 7, 7
	END	
	
	IF FLAG $PUERTA = 81
	THEN
		WARP_TO 1, 11, 7
	END	

	IF FLAG $PUERTA = 83
	THEN
		WARP_TO 15, 3, 7
	END	
	
	IF FLAG $PUERTA = 84
	THEN
		WARP_TO 0, 2, 4
	END	
	
	IF FLAG $PUERTA = 22 ## PUERTA COMUN DE ENTRADA PARA TODOS LOS BOSSES, CUANDO LOS DERROTAMOS, SALIMOS AL PUEBLO POR AQUI (EXCEPTO EL DE MARIO)
	THEN
		WARP_TO 12, 7, 2
	END	
	
END

ENTERING ANY
	IF TRUE
	THEN
		TIMER_STOP ##NO HAY BANDERINES
	END
END

ENTERING SCREEN 3
	IF $PANTPRO = 5
	THEN
		MUSIC 1
		SET $PANTPRO = 3
	END
END

ENTERING SCREEN 8
	IF $SCHOOL > 15
	THEN
		DECORATIONS
			0, 8, 12
			1, 8, 12
			2, 8, 12
			3, 8, 12
			4, 8, 12
			5, 8, 12
			6, 8, 12
			7, 8, 12
			8, 8, 12
			9, 8, 12
			10, 8, 12
		END
	END	
END

ENTERING SCREEN 12
IF FLAG $PUERTA = 22
	THEN 
		DECORATIONS
			7, 1, 24
			7, 2, 24
		END
		SET FLAG $PUERTA = 0
	END
END


ENTERING SCREEN 15
	IF FLAG $SCHOOL > 16
	THEN 
		DECORATIONS
			3, 6, 7
			3, 7, 23
		END
	END
	
	IF FLAG $SCHOOL > 21
	THEN 
		DECORATIONS
			7, 6, 7
			7, 7, 23
		END
	END
	
	IF TRUE
	THEN
		SET $VUELTA = 0
		TIMER_START
		SET_TIMER 1, 15 ## SET TIMER FOR CARTEL HOTEL 
	END
	
END

ENTERING SCREEN 4
	IF TRUE
	THEN
		SET $VUELTA = 0
		TIMER_START
		SET_TIMER 1, 1 ## SET TIMER FOR FLAGS IN CASTLE
	END
END

ENTERING SCREEN 18
	IF TRUE
	THEN
		SET $VUELTA = 0
		TIMER_START
		SET_TIMER 1, 1 ## SET TIMER FOR FLAGS IN CASTLE
	END
END

### TIMER FOR FLAGS IN CASTLE

ON_TIMER_OFF

	## ANIMACION BANDERAS CASTILLO
	
	IF NPANT 4
	IF $VUELTA = 0
	THEN
		DECORATIONS
			1, 2, 34
			13, 2, 34
		END
		SET $VUELTA = 1
		SET_TIMER 1, 3	
		BREAK		
	END
	
	IF NPANT 4
	IF $VUELTA = 1
	THEN
		DECORATIONS
			1, 2, 35
			13, 2, 35
		END
		SET $VUELTA = 0
		SET_TIMER 1, 3	
		BREAK
	END	
	

	IF NPANT 18
	IF $VUELTA = 0
	THEN
		DECORATIONS
			8, 1, 34
			10, 1, 34
			12, 1, 34
			14, 1, 34
		END
		SET $VUELTA = 1
		SET_TIMER 1, 3	
		BREAK		
	END
	
	IF NPANT 18
	IF $VUELTA = 1
	THEN
		DECORATIONS
			8, 1, 35
			10, 1, 35
			12, 1, 35
			14, 1, 35
		END
		SET $VUELTA = 0
		SET_TIMER 1, 3	
		BREAK
	END
	
	## ANIMACION CARTEL HOTEL
	
	IF NPANT 15
	IF $VUELTA = 0
	THEN
		DECORATIONS
			2, 1, 36
			3, 1, 37
		END
		SET $VUELTA = 1
		SET_TIMER 1, 3	
		BREAK		
	END
	
	IF NPANT 15
	IF $VUELTA = 1
	THEN
		DECORATIONS
			2, 1, 38
			3, 1, 39
		END
		SET $VUELTA = 2
		SET_TIMER 1, 3	
		BREAK
	END
	
	IF NPANT 15
	IF $VUELTA = 2
	THEN
		DECORATIONS
			2, 1, 17
			3, 1, 18
		END
		SET $VUELTA = 0
		SET_TIMER 1, 30	
		BREAK
	END
	
END


#####################################
## EVENTOS PUEBLO (PUERTAS, LEER, ETC)
#####################################


PRESS_FIRE AT SCREEN 0
	IF PLAYER_TOUCHES 1, 7
	THEN
		WARP_TO 10, 1, 7
	END
	
	IF PLAYER_TOUCHES 4, 4
	THEN
		EXTERN 51 #cartel PUERTA SPACE
		REDRAW
	END
	
	## PUERTA SPACE (GRADIUS)
	IF PLAYER_TOUCHES 2, 4
	THEN
		SET FLAG $PUERTA = 84
		DECORATIONS
			2, 4, 24
			2, 3, 24
		END
		SHOW
		MUSIC 5
		PAUSE 50
		WARP_TO_LEVEL 8, 1, 2, 5, 1
	END
	
	
	
END

PRESS_FIRE AT SCREEN 1

	IF PLAYER_TOUCHES 9, 7
	THEN
		EXTERN 17 #cartel PUERTA melon beach
		REDRAW
	END

	## PUERTA WBOY 1
	IF PLAYER_TOUCHES 11, 7
	THEN
		SET FLAG $PUERTA = 81
		DECORATIONS
			11, 7, 24
			11, 6, 24
		END
		SHOW
		MUSIC 5
		PAUSE 50
		WARP_TO_LEVEL 8, 1, 2, 5, 1
	END
END

PRESS_FIRE AT SCREEN 4

	IF PLAYER_TOUCHES 8, 8
	THEN
		EXTERN 45 #cartel PUERTA WB3
		REDRAW
	END
	
	## PUERTA WONDER BOY 3 WB3
	IF $SCHOOL > 25
	IF PLAYER_TOUCHES 10, 8
	THEN
		SET FLAG $PUERTA = 6
		DECORATIONS
			10, 7, 24
			10, 8, 24
		END
		SHOW
		MUSIC 5
		PAUSE 50
		WARP_TO_LEVEL 4, 1, 2, 5, 1
	END
	
	IF PLAYER_TOUCHES 10, 8 #NO TENEMOS LA LLAVE PERO ESTAMOS DELANTE DE LA PUERTA = NO SE ABRE
	THEN
		EXTERN 18 # PUERTA CERRADA
		REDRAW
	END
	
	## ACCESOS A LOS FINALES
	
		## FINAL 
	IF $SCHOOL = 30
	IF PLAYER_IN_0 16, 224
	IF PLAYER_IN_Y 0, 64
	THEN
		SOUND 17
		EXTERN 70
		SET FLAG $PUERTA = 0
		SET FLAG $PROGRESO = 0
		SET FLAG $PODERDISPARO = 0
		SET FLAG $BOLAS = 0
		GAME_ENDING
		BREAK
	END
		
	IF $SCHOOL = 27
	IF PLAYER_IN_0 16, 224
	IF PLAYER_IN_Y 0, 64
	THEN
		SOUND 17
		EXTERN 69
		SET FLAG $PUERTA = 0
		SET FLAG $PROGRESO = 0
		SET FLAG $PODERDISPARO = 0
		SET FLAG $BOLAS = 0
		GAME_ENDING
		BREAK
	END

	
	## FIN ACCESOS FINALES
	
END

PRESS_FIRE AT SCREEN 5

	
	IF PLAYER_TOUCHES 4, 3
	THEN
		EXTERN 50 #cartel PUERTA CIELO
		REDRAW
	END
	
	## PUERTA CIELO
	IF $SCHOOL > 18
	IF PLAYER_TOUCHES 2, 3
	THEN
		SET FLAG $PANTPRO = 5
		DECORATIONS
			2, 2, 24
			2, 3, 24
		END
		SHOW
		MUSIC 5
		PAUSE 50
		WARP_TO 3, 8, 2
	END
	
	IF PLAYER_TOUCHES 2, 3 #NO TENEMOS LA LLAVE PERO ESTAMOS DELANTE DE LA PUERTA = NO SE ABRE
	THEN
		EXTERN 18 # PUERTA CERRADA
		REDRAW
	END
	
END

PRESS_FIRE AT SCREEN 7

	
	IF PLAYER_TOUCHES 6, 7
	THEN
		EXTERN 48 #cartel PUERTA G&G
		REDRAW
	END
	
	## PUERTA GHOULS & GOBLINS
	IF $SCHOOL > 17
	IF PLAYER_TOUCHES 4, 7
	THEN
		SET FLAG $PUERTA = 71
		DECORATIONS
			4, 7, 24
			4, 6, 24
		END
		SHOW
		MUSIC 5
		PAUSE 50
		WARP_TO_LEVEL 7, 1, 2, 5, 1
	END
	
	IF PLAYER_TOUCHES 4, 7 #NO TENEMOS LA LLAVE PERO ESTAMOS DELANTE DE LA PUERTA = NO SE ABRE
	THEN
		EXTERN 18 # PUERTA CERRADA
		REDRAW
	END
	
END

PRESS_FIRE AT SCREEN 8
	IF PLAYER_TOUCHES 13, 8
	THEN
		EXTERN 6#cartel SALTO DE FE
		REDRAW
	END
	
	IF $SCHOOL = 15
	IF PLAYER_TOUCHES 11, 6
	THEN
		SOUND 7
		PAUSE 15
		SOUND 8
		DECORATIONS
			0, 8, 12
			1, 8, 12
			2, 8, 12
			3, 8, 12
			4, 8, 12
			5, 8, 12
			6, 8, 12
			7, 8, 12
			8, 8, 12
			9, 8, 12
			10, 8, 12
		END
		SHOW
		SET $SCHOOL = 16
		SET $PROGRESO = 16
	END	
	
	

END

##PRESS_FIRE AT SCREEN 9

##END

PRESS_FIRE AT SCREEN 10
	IF PLAYER_TOUCHES 1, 7
	THEN
		WARP_TO 0, 1, 7
	END
END

PRESS_FIRE AT SCREEN 14

	## salir de la torre
	IF PLAYER_TOUCHES 3, 8
	THEN
		WARP_TO 19, 7, 7
	END
	
	IF PLAYER_TOUCHES 5, 8
	THEN
		EXTERN 44 #cartel PUERTA CASTLEVANIA
		REDRAW
	END
	
	## PUERTA CASTLEVANIA
	IF PLAYER_TOUCHES 7, 8
	THEN
		SET FLAG $PUERTA = 4
		DECORATIONS
			7, 7, 24
			7, 8, 24
		END
		SHOW
		MUSIC 5
		PAUSE 50
		WARP_TO_LEVEL 3, 1, 2, 5, 1
	END
	
END


PRESS_FIRE AT SCREEN 15

	IF $SCHOOL > 2 ## PUERTA SHINOBI DESBLOQUEADA - TENEMOS LA LLAVE NINJA
	IF PLAYER_TOUCHES 7, 2 ## PERSONAJE DELANTE DE PUERTA SHINOBI
	THEN
		SET FLAG $PUERTA = 5
		DECORATIONS
			7, 1, 24
			7, 2, 24
		END
		SHOW
		MUSIC 5
		PAUSE 50
		WARP_TO_LEVEL 5, 1, 2, 5, 1
	END
	
	IF $SCHOOL > 16 ## PUERTA SECRETA DESBLOQUEADA (ACTIVAMOS LA PALANCA DEL WB1)
	IF PLAYER_TOUCHES 3, 7 ## PERSONAJE DELANTE DE PUERTA SECRETA
	THEN
		SET FLAG $PUERTA = 83 ## AUNQUE NO ESTE EN NIVEL 8, LA ASOCIO PORQUE VENIMOS DE EL.
		DECORATIONS
			3, 6, 24
			3, 7, 24
		END
		SHOW
		MUSIC 5
		PAUSE 50
		WARP_TO_LEVEL 6, 1, 2, 5, 1
	END
	
	IF $SCHOOL > 21 ## PUERTA SECRETA 2 DESBLOQUEADA (ACCESO AL LABERINTO)
	IF PLAYER_TOUCHES 7, 7 ## PERSONAJE DELANTE DE PUERTA SECRETA
	THEN
		SET FLAG $PUERTA = 79 ## AUNQUE NO ESTE EN NIVEL 7, LA ASOCIO PORQUE VENIMOS DE EL.
		DECORATIONS
			7, 6, 24
			7, 7, 24
		END
		SHOW
		MUSIC 5
		PAUSE 50
		WARP_TO_LEVEL 6, 1, 2, 5, 1
	END

	IF PLAYER_TOUCHES 7, 2 #NO TENEMOS LA LLAVE NINJA PERO ESTAMOS DELANTE DE LA PUERTA = NO SE ABRE
	THEN
		EXTERN 18 # PUERTA CERRADA
		REDRAW
	END
	
END

PRESS_FIRE AT SCREEN 16

	IF $SCHOOL > 0 ## PUERTA SONIC DESBLOQUEADA
	IF PLAYER_TOUCHES 7, 7 ## PERSONAJE DELANTE DE PUERTA SONIC
	#SI TENEMOS LA LLAVE Y ESTAMOS DELANTE DE LA PUERTA = SE ABRE
	THEN
			SET FLAG $PUERTA = 1
			DECORATIONS
				7, 6, 24
				7, 7, 24
			END
			SHOW
			MUSIC 5
			PAUSE 50
			WARP_TO_LEVEL 1, 1, 2, 5, 1
	END

	IF PLAYER_TOUCHES 7, 7 #NO TENEMOS LA LLAVE PERO ESTAMOS DELANTE DE LA PUERTA = NO SE ABRE
	THEN
		EXTERN 18 # PUERTA CERRADA
		REDRAW
	END
	
	IF PLAYER_TOUCHES 9, 7 #CARTEL SONIC
	THEN
		EXTERN 3
		REDRAW
	END	
	
	IF PLAYER_TOUCHES 1, 7 #CARTEL HOTEL
	THEN
		EXTERN 2
	REDRAW
	END	
	
END

PRESS_FIRE AT SCREEN 17

	IF PLAYER_TOUCHES 1, 7
	THEN
		EXTERN 1#cartel bienvenida
		REDRAW
	END

	IF $SCHOOL > 0 ## PUERTA MARIO DESBLOQUEADA
	IF PLAYER_TOUCHES 7, 7 ## PERSONAJE DELANTE DE PUERTA MARIO
	#SI TENEMOS LA LLAVE Y ESTAMOS DELANTE DE LA PUERTA = SE ABRE
	THEN
			SET FLAG $PUERTA = 2
			DECORATIONS
				7, 6, 24
				7, 7, 24
			END
			SHOW
			MUSIC 5
			PAUSE 50
			WARP_TO_LEVEL 2, 0, 2, 5, 1
	END

	IF PLAYER_TOUCHES 7, 7 #NO TENEMOS LA LLAVE PERO ESTAMOS DELANTE DE LA PUERTA = NO SE ABRE
	THEN
		EXTERN 18 # PUERTA CERRADA
		REDRAW
	END

	IF PLAYER_TOUCHES 9, 7 #cartel MARIO
	THEN
		EXTERN 4
		REDRAW
	END			

				
END

PRESS_FIRE AT SCREEN 18
	
	IF PLAYER_TOUCHES 13, 7
	THEN
		EXTERN 42#cartel enfermeria
		REDRAW
	END
	
	IF $SCHOOL > 2
	IF PLAYER_TOUCHES 11, 7 ##PUERTA ENFERMERIA
	THEN
		SET FLAG $PUERTA = 7
		DECORATIONS
			11, 6, 24
			11, 7, 24
		END
		SHOW
		MUSIC 5
		PAUSE 50
		WARP_TO_LEVEL 6, 0, 2, 8, 1
	END	
	
	IF PLAYER_TOUCHES 11, 7 ##PUERTA ENFERMERIA CERRADA
	THEN
		EXTERN 18 # PUERTA CERRADA
		REDRAW
	END

	IF $SCHOOL < 27
	IF PLAYER_TOUCHES 3, 7 ##PUERTA SCHOOL
	THEN
		SET FLAG $PUERTA = 60
		DECORATIONS
			3, 6, 24
			3, 7, 24
		END
		SHOW
		MUSIC 5
		PAUSE 50
		WARP_TO_LEVEL 6, 0, 2, 8, 1
	END	
	
	IF PLAYER_TOUCHES 3, 7 ##PUERTA SCHOOL CERRADA
	THEN
		EXTERN 18 # PUERTA CERRADA
		REDRAW
	END
	
END

PRESS_FIRE AT SCREEN 19

	IF $SCHOOL > 5 ## PUERTA TORRE DESBLOQUEADA
	IF PLAYER_TOUCHES 7, 7 ## PERSONAJE DELANTE DE PUERTA TORRE
	#SI TENEMOS LA LLAVE Y ESTAMOS DELANTE DE LA PUERTA = SE ABRE
	THEN
		SET FLAG $PUERTA = 4
		DECORATIONS
			7, 6, 24
			7, 7, 24
		END
		SHOW
		MUSIC 5
		PAUSE 50
		MUSIC 1
		WARP_TO 14, 3, 8
	END

	IF PLAYER_TOUCHES 5, 7
	THEN
		EXTERN 43 #cartel TORRE
		REDRAW
	END

	IF PLAYER_TOUCHES 7, 7 #NO TENEMOS LA LLAVE PERO ESTAMOS DELANTE DE LA PUERTA = NO SE ABRE
	THEN
		EXTERN 18 # PUERTA CERRADA
		REDRAW
	END

END

END_OF_LEVEL
