##############################################################################
# LEVEL 6 ROOMS - SECRETS ROOMS AND BOSSES

# ATENCION: EN ESTE NIVEL, COMO HAY MUCHAS PUERTAS, EL NUMERO DE $PUERTA NO CAMBIA,
# ES EL MISMO DESDE LA QUE SE LLEGÓ A LA HABITACION. LOS DEMAS NIVELES SI SE NUMERAN
# COMO NºNIVEL*10 + Nº PUERTA. EJEMPLO: PUERTA 2 DEL NIVEL 3 SERÍA "32" (3*10 + 2)
#
##############################################################################
LEVELID LEVEL_6
INC_DECORATIONS decorations6.spt

ENTERING GAME

	##SET $NIVELPRO = 6
	
	## REDIRECCIONES A LAS HABITACIONES CORRESPONDIENTES
	
	IF $PUERTA = 52 #room Calva Magica
	THEN
		WARP_TO 2, 3, 8
	END
	
	IF $PUERTA = 31 #room BELMONT 
	THEN
		WARP_TO 4, 12, 6
	END
	
	IF $PUERTA = 32 #room PALANCAS 
	THEN
		WARP_TO 1, 3, 8
	END
	
	IF FLAG $PUERTA = 36 #room SECRETA ANTES DEL BOSS DE CASTLEVANIA
	THEN
	WARP_TO 5, 3, 8
	END	
	
	IF FLAG $PUERTA = 7 #room ENFERMERIA (NURSING)
	THEN
	WARP_TO 7, 7, 2
	END	
	
	IF FLAG $PUERTA = 49 #room FINAL CON MAESTRO TORTUGA
	THEN
	WARP_TO 19, 4, 8
	END	

	IF FLAG $PUERTA = 72 #room SECRETA from G&G, puerta hotel 2
	THEN
	WARP_TO 17, 1, 2
	END	
	
	IF FLAG $PUERTA = 73 #room SECRETA from G&G, llave del cielo
	THEN
	WARP_TO 13, 1, 7
	END	

	IF FLAG $PUERTA = 79 #LABERINTO
	THEN
	WARP_TO 15, 2, 8
	END	
	
	IF FLAG $PUERTA = 82 #room WB1 PALANCA ACTIVA PUERTA SECRETA HOTEL
	THEN
	WARP_TO 8, 13, 8
	END	
	
	IF FLAG $PUERTA = 83 #room SECRETA JUNTO A CALVA, llave del cementerio
	THEN
	WARP_TO 3, 2, 3
	END	
	
	IF FLAG $PUERTA = 85 #room BOLA DE LOS ESPIRITUS, descubre las tumbas del cementerio
	THEN
	WARP_TO 14, 1, 1
	END	
	
END

ENTERING SCREEN 2

	IF TRUE
	THEN
		PRINT_TILE_AT (29,12) = 2
	END

END 
ENTERING SCREEN 3

	IF $SCHOOL > 17
	THEN
	SET TILE (11, 8)= 3
	END
END
ENTERING SCREEN 4

	IF $SCHOOL > 11
	THEN
		SET TILE (4,7) = 3
		SET TILE (4,8) = 3
		SET TILE (13,8) = 2
		PRINT_TILE_AT (27,16) = 2
	END
	

END

ENTERING SCREEN 5

	IF $SCHOOL < 27
	THEN
		PRINT_TILE_AT (29,8) = 2
		PRINT_TILE_AT (29,10) = 2
	END

END

ENTERING SCREEN 8
	IF TRUE
	THEN
		PRINT_TILE_AT (29,6) = 2
	END

	IF $SCHOOL > 16
	THEN
		SET TILE (13,1)= 18
		SHOW
	END

END

ENTERING SCREEN 12
	IF $SCHOOL > 22
	THEN
		SET TILE (9,5)= 18
	SHOW
	END
	
	IF $SCHOOL > 26
	IF $PROCESOR1 < 1
	THEN
		SET TILE (12,8)= 26
	SHOW
	END
	
END

ENTERING SCREEN 13
	IF $SCHOOL > 18
	THEN
	SET TILE (11, 8)= 3
	SHOW
	END
END

ENTERING SCREEN 17
	IF $SCHOOL > 21
	THEN
		SET TILE (9,8)= 18
		SHOW
	END
	
	IF $SCHOOL > 26
	IF $PROCESOR2 < 1
	THEN
		SET TILE (8, 8)= 27
		SHOW
	END
END

ENTERING SCREEN 7

	IF TRUE
	THEN
		EXTERN 46# HABLA LA ENFERMERA
		RECHARGE 
		WARP_TO_LEVEL 0, 0, 2, 5, 1
	END

END


### EVENTOS ######

PRESS_FIRE AT SCREEN 1 ## ROOM PALANCAS



	IF PLAYER_TOUCHES 2, 8 ## PUERTA DE SALIDA
	THEN
		SET $PUERTA = 32 # PUERTA PALANCAS
		WARP_TO_LEVEL 3, 0, 2, 5, 1
	END	

	IF $SCHOOL > 7 ## SI YA HEMOS PASADO LA PRUEBA NO PASA NADA
	THEN
		BREAK
	END
	
	IF $PALANCA3 = 0
	IF PLAYER_TOUCHES 8, 4 ## PALANCA 3
	THEN
		SET TILE (8,4)= 18
		SET $PALANCA3 = 1
		BREAK
	END
	IF PLAYER_TOUCHES 8, 4
	THEN
		SET TILE (8,4)= 18
		SET $BOSSLIFE = 99
	END	
	
	IF $PALANCA3 = 1
	IF PLAYER_TOUCHES 10, 4 ## PALANCA 4
	THEN
		SET TILE (10,4)= 18
		SET $PALANCA4 = 1
		BREAK
	END	
	IF PLAYER_TOUCHES 10, 4
	THEN
		SET TILE (10,4)= 18
		SET $BOSSLIFE = 99
	END	
	
	IF $PALANCA4 = 1
	IF PLAYER_TOUCHES 6, 4 ## PALANCA 2
	THEN
		SET TILE (6,4)= 18
		SET $PALANCA2 = 1
		BREAK
	END
	IF PLAYER_TOUCHES 6, 4
	THEN
		SET TILE (6,4)= 18
		SET $BOSSLIFE = 99
	END	
	
	IF $BOSSLIFE = 0
	IF $PALANCA2 = 1
	IF PLAYER_TOUCHES 4, 4 ## PALANCA 1
	THEN
		SET TILE (4,4)= 18
		SET $PALANCA1 = 1
		EXTERN 29# OBTENEMOS LA BOLA DE PIEDRA
		SET $SCHOOL = 8 
		REDRAW
		SET $BOSSLIFE = 99
	END
	IF PLAYER_TOUCHES 4, 4
	THEN
		SET TILE (4,4)= 18
		SET $BOSSLIFE = 99
	END
	

END

PRESS_FIRE AT SCREEN 3 ## LLAVE CEMENTERIO

	IF PLAYER_TOUCHES 2, 3 ## PUERTA DE SALIDA
	THEN
		SET FLAG $PUERTA = 83
		WARP_TO_LEVEL 0, 5, 11, 4, 1
	END	
	
	IF $SCHOOL = 17
	IF PLAYER_TOUCHES 11, 8 ## LLAVE
	THEN
		SET FLAG $SCHOOL = 18
		SOUND 8
		SET TILE (11, 8)= 3
	END	

	IF PLAYER_TOUCHES 9, 8 ## PALANCA
	THEN
		SOUND 9
		SET TILE (7, 5)= 3
		SET TILE (9, 8)= 18
		SHOW
		PAUSE 20
		SET TILE (7, 6)= 3
		SHOW
		PAUSE 20
	END	
END

PRESS_FIRE AT SCREEN 4 ## PACO BELMONT

	IF PLAYER_TOUCHES 12, 6 ## PUERTA DE SALIDA
	THEN
		SET FLAG $PUERTA = 31
		WARP_TO_LEVEL 3, 5, 11, 4, 1
	END	

	IF $SCHOOL = 11
	THEN
		EXTERN 34 #HABLA BELMONT
		EXTERN 35
		EXTERN 36
		REDRAW
		EXTERN 37
		REDRAW
		SET $SCHOOL = 12
	END	

END

PRESS_FIRE AT SCREEN 8 ## WB1 - PALANCA ABRE PUERTA HOTEL - LLAVE CEMENTERIO

	IF PLAYER_TOUCHES 13, 8 ## PUERTA DE SALIDA
	THEN
		SET FLAG $PUERTA = 82
		WARP_TO_LEVEL 8, 9, 11, 4, 1
	END	

	IF $SCHOOL = 16
	IF PLAYER_TOUCHES 13, 1 ## PALANCA
	THEN
		SOUND 9
		SET TILE (13,1)= 18
		SHOW
		EXTERN 49 #MENSAJE PUERTA ABIERTA EN PUEBLO
		REDRAW
		PRINT_TILE_AT (29,6) = 2
		SHOW
		SET $SCHOOL = 17
	END	

END


PRESS_FIRE AT SCREEN 13 ## LLAVE CIELO

	IF PLAYER_TOUCHES 1, 7 ## PUERTA DE SALIDA
	THEN
		SET FLAG $PUERTA = 73
		WARP_TO_LEVEL 7, 5, 11, 4, 1
	END	
	
	IF $SCHOOL = 18
	IF PLAYER_TOUCHES 11, 8 ## LLAVE
	THEN
		SET FLAG $SCHOOL = 19
		SOUND 8
		SET TILE (11, 8)= 3
	END	

END

PRESS_FIRE AT SCREEN 14 ## bola espiritus

	IF PLAYER_TOUCHES 1, 1 ## PUERTA DE SALIDA
	THEN
		SET FLAG $PUERTA = 85
		WARP_TO_LEVEL 8, 5, 11, 4, 1
	END	
	
	IF $SCHOOL = 19
	IF PLAYER_TOUCHES 12, 8 ## obtenemos la bola spiritus del marciano
	THEN
		EXTERN 52
		REDRAW
		EXTERN 53
		REDRAW
		SET FLAG $SCHOOL = 20
		RECHARGE
		SOUND 8
	END	

END

##### LABERINTO

PRESS_FIRE AT SCREEN 15 ## ENTRADA AL LABERINTO
	IF PLAYER_TOUCHES 2, 8 ## PUERTA DE SALIDA
	THEN
		SET FLAG $PUERTA = 79
		WARP_TO_LEVEL 0, 5, 11, 4, 1
	END	
END

PRESS_FIRE AT SCREEN 16
	IF PLAYER_TOUCHES 5, 8
	THEN
		SOUND 15
		WARP_TO 10, 4, 7
	END	
	
	IF PLAYER_TOUCHES 3, 5
	THEN
		SOUND 15
		WARP_TO 16, 12, 8
	END		
	
	IF PLAYER_TOUCHES 12, 8
	THEN
		SOUND 15
		WARP_TO 16, 3, 5
	END	
	
END

PRESS_FIRE AT SCREEN 10
	IF PLAYER_TOUCHES 4, 7
	THEN
		SOUND 15
		WARP_TO 16, 5, 8
	END	

	IF PLAYER_TOUCHES 12, 3
	THEN
		SOUND 15
		WARP_TO 15, 2, 8
	END	
	
END

PRESS_FIRE AT SCREEN 11
	IF PLAYER_TOUCHES 5, 2
	THEN
		SOUND 15
		WARP_TO 16, 3, 5
	END	

	IF PLAYER_TOUCHES 12, 3
	THEN
		SOUND 15
		WARP_TO 12, 2, 7
	END	
	
END

PRESS_FIRE AT SCREEN 12
	IF PLAYER_TOUCHES 2, 7
	THEN
		SOUND 15
		WARP_TO 11, 12, 3
	END	

	IF $SCHOOL = 22
	IF PLAYER_TOUCHES 9,5 ## PALANCA
	THEN
		SOUND 9
		SET TILE (9,5)= 18
		SHOW
		EXTERN 55 #MENSAJE PUERTA ABIERTA EN PUEBLO
		REDRAW
		SET $SCHOOL = 23
	END	
	
	IF $SCHOOL > 26
	IF $PROCESOR1 < 1
	IF PLAYER_TOUCHES 12, 8 ## PUERTA DE SALIDA
	THEN
		SOUND 8
		EXTERN 68
		REDRAW
		SET FLAG $PROCESOR1 = 1
		SET TILE (12,8)= 3
		SHOW
	END	
	
END

##### FIN LABERINTO

PRESS_FIRE AT SCREEN 17 ## PALANCA ABRE PUERTA HOTEL 2 (que va al laberinto)

	IF PLAYER_TOUCHES 1,2 ## PUERTA DE SALIDA
	THEN
		SET FLAG $PUERTA = 72
		WARP_TO_LEVEL 7, 9, 11, 4, 1
	END	

	IF $SCHOOL = 21
	IF PLAYER_TOUCHES 9,8 ## PALANCA
	THEN
		SOUND 9
		SET TILE (9,8)= 18
		SHOW
		EXTERN 49 #MENSAJE PUERTA ABIERTA EN PUEBLO
		REDRAW
		SET $SCHOOL = 22
	END	
	
	### procesor 2
	IF $SCHOOL > 26
	IF $PROCESOR2 < 1
	IF PLAYER_TOUCHES 8, 8
	THEN
		SOUND 8
		EXTERN 68
		REDRAW
		SET FLAG $PROCESOR2 = 1
		SET TILE (8, 8)= 3
		SHOW
	END	

END


PRESS_FIRE AT SCREEN 5 ## room secreta castlevania

	IF PLAYER_TOUCHES 3, 8 ## PUERTA DE SALIDA abajo
	THEN
		WARP_TO_LEVEL 3, 5, 11, 4, 1
	END	
	
	IF PLAYER_TOUCHES 3, 3 ## PUERTA DE SALIDA arriba (trampa hacia room belmont)
	THEN
		SOUND 13
		SET FLAG $PUERTA = 31
		WARP_TO 4, 2, 2
	END	
	

END


PRESS_FIRE AT SCREEN 2 ## CALVA MAGICA

	IF PLAYER_TOUCHES 3, 8 ## PUERTA DE SALIDA
	THEN
		SET FLAG $PUERTA = 52
		WARP_TO_LEVEL 5, 5, 11, 4, 1
	END	
	
	IF $SCHOOL = 27
	THEN
		EXTERN 71 #PISTA PIEZA Z80 MAS DIFICIL
		REDRAW
		PRINT_TILE_AT (29,12) = 2
		BREAK
	END	

	IF $SCHOOL = 7
	THEN
		EXTERN 27 #PISTA COMBINACION PALANCAS
		REDRAW
		PRINT_TILE_AT (29,12) = 2
		BREAK
	END	
	
	IF $SCHOOL = 3
	THEN
		EXTERN 22 #TE DA LA BOLA DE FUEGO
		EXTERN 23
		REDRAW
		PRINT_TILE_AT (29,12) = 2
		SET $SCHOOL = 4
		BREAK
	END	
	
	IF $SCHOOL = 18
	THEN
		EXTERN 73#DESPUES DE PILLAR LA LLAVE Y APARECER POR DETRAS
		REDRAW
		PRINT_TILE_AT (29,12) = 2
		BREAK
	END	
	
	IF TRUE
	THEN
		EXTERN 74# UHMMM
		REDRAW
		PRINT_TILE_AT (29,12) = 2
		BREAK
	END	
	
END

PRESS_FIRE AT SCREEN 0 ## MAESTRO TORTUGA

	
	IF PLAYER_TOUCHES 3, 8 ## PUERTA DE SALIDA
	THEN
		SET FLAG $PUERTA = 60
		WARP_TO_LEVEL 0, 18, 3, 7, 1
	END	

#### DIALOGOS TORTUGA DUENDE - ORDEN DE ABAJO A ARRIBA -
#### EL FLAG $SCHOOL INDICA QUE TEXTOS NOS DIRÁ EL MAESTRO. CUANDO SE AVANZA EN LA HISTORIA SE VAN SUMANDO +1 AL FLAG.

	IF $SCHOOL = 25
	THEN
		EXTERN 60#EXPLICACION BOLA DE DRAGON
		EXTERN 61
		REDRAW
		SET $SCHOOL = 26
		SET $PROGRESO = 26
		SET $BOLAS = 6
		SET $PODERDISPARO = 14
		BREAK
	END

	IF $SCHOOL = 23
	THEN
		EXTERN 56#EXPLICACION BOLA ESPIRITUAL 2 (TRUCO DISPARO EN CAVERNA)
		EXTERN 57
		EXTERN 58
		REDRAW
		SET $SCHOOL = 24
		SET $PROGRESO = 24
		BREAK
	END

	IF $SCHOOL = 20
	THEN
		EXTERN 54#EXPLICACION BOLA ESPIRITUAL
		REDRAW
		SET $SCHOOL = 21
		SET $PROGRESO = 21
		SET $BOLAS = 5
		SET $PODERDISPARO = 14
		BREAK
	END

	IF $SCHOOL = 14
	THEN
		EXTERN 41#EXPLICACION CRUZ PENITENTE
		REDRAW
		SET $SCHOOL = 15
		SET $PROGRESO = 15
		BREAK
	END

	IF $SCHOOL = 12
	THEN
		EXTERN 39#EXPLICACION BOLA DE AGUA BENDITA (PARA ROMPER CRUCES)
		REDRAW
		SET $SCHOOL = 13
		SET $PROGRESO = 13
		SET $BOLAS = 4
		SET $PODERDISPARO = 12
		BREAK
	END	

	IF $SCHOOL = 10
	THEN
		EXTERN 33#EXPLICACION BOLA DE VIENTO (PARA APAGAR ANTORCHAS)
		REDRAW
		SET $SCHOOL = 11
		SET $PROGRESO = 11
		SET $BOLAS = 3
		SET $PODERDISPARO = 10
		BREAK
	END	

	IF $SCHOOL = 8
	THEN
		EXTERN 30#EXPLICACION BOLA DE PIEDRA (PARA SHINOBI WORLD)
		REDRAW
		SET $SCHOOL = 9
		SET $BOLAS = 2
		SET $PODERDISPARO = 8
		SET $PROGRESO = 9
		BREAK
	END	

	IF $SCHOOL = 6
	THEN
		EXTERN 26#VISITA CALVA PARA QUE NOS DIGA SECRETOS DE TORRE
		REDRAW
		SET $SCHOOL = 7
		SET $PROGRESO = 7
		BREAK
	END	
	
	IF $SCHOOL = 4
	THEN
		EXTERN 24#explicacion bola de fuego
		REDRAW
		SET $BOLAS = 1
		SET $PODERDISPARO = 5
		SET $SCHOOL = 5
		SET $PROGRESO = 5
		BREAK
	END	

	IF $SCHOOL = 2
	THEN
		EXTERN 19#explicacion mision shinobi
		EXTERN 20
		EXTERN 21
		REDRAW
		EXTERN 47
		REDRAW
		SET $SCHOOL = 3
		SET $PROGRESO = 3
		BREAK
	END	


	IF $SCHOOL = 0
	THEN
		EXTERN 7#bienvenido a school
		EXTERN 8#y parrafada inicial
		EXTERN 9
		EXTERN 10
		REDRAW
		EXTERN 11
		REDRAW
		EXTERN 12
		REDRAW
		EXTERN 72
		REDRAW
		EXTERN 13
		REDRAW
		SET $SCHOOL = 1
		SET $PROGRESO = 1
		
		## quitar lo de abajo de esta linea
		#SET $SCHOOL = 27
		#SET $PROGRESO = 27
		#SET $PODERDISPARO = 12
		#SET $BOLAS = 7
	
		#GAME_ENDING

		BREAK
	END
	
	IF TRUE
	THEN
		EXTERN 14#vamooooos
		REDRAW
	END	
	
END

######## HABITACION FINAL CON TORTUGA

PRESS_FIRE AT SCREEN 19

	IF TRUE
	THEN
		EXTERN 62# HABLAS CON TORTUGA
		EXTERN 63
		REDRAW
		EXTERN 64
		REDRAW
		EXTERN 65
		REDRAW
		EXTERN 66
		REDRAW
		EXTERN 67
		MUSIC 5
		PAUSE 50
		SET $PUERTA = 22
		SET $SCHOOL = 27
		SET $BOLAS = 7
		WARP_TO_LEVEL 0, 0, 2, 5, 1
	END

END

END_OF_LEVEL

