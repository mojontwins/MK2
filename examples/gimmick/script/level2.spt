##############################################################################
# LEVEL 2 MARIO 
##############################################################################
INC_DECORATIONS decorations2.spt

ENTERING GAME
		IF TRUE
		THEN
		SET $PUERTA = 0
		##SET $NIVELPRO = 2
		###WARP_TO 16, 1, 7 ## ACTIVAR PARA IR DIRECTO AL JEFE
		END
END

ENTERING ANY
	IF TRUE
	THEN
	SET $PUERTA = 0
	END
END


ENTERING SCREEN 15

	IF $SCHOOL > 26
	IF $PROCESOR3 < 1
	THEN
		SET TILE (7, 4)= 32
		SHOW
	END
	
END

ENTERING SCREEN 17

	IF TRUE
	THEN
		SET $PANTPRO = 17
	END
	
END

## EVENTOS FIRE KEY MARIO ##


PRESS_FIRE AT SCREEN 0
	IF PLAYER_TOUCHES 1, 8 ## PUERTA DE SALIDA 1
	THEN
		SET FLAG $PUERTA = 21
		WARP_TO_LEVEL 0, 0, 2, 5, 1
	END	
END

PRESS_FIRE AT SCREEN 11
	IF PLAYER_TOUCHES 7, 7 ## PUERTA DE ENTRADA AL JEFE 1
	THEN
		MUSIC 5
		PAUSE 50
		SET $PANTPRO = 11
		WARP_TO 16, 1, 7
	END	
END


PRESS_FIRE AT SCREEN 15
	IF PLAYER_TOUCHES 12, 7 ## PUERTA DE ENTRADA AL JEFE 2
	THEN
		MUSIC 5
		PAUSE 50
		SET $PANTPRO = 15
		WARP_TO 16, 1, 7
	END	
	
	IF $SCHOOL > 26
	IF $PROCESOR3 < 1
	IF PLAYER_TOUCHES 7, 4
	THEN
		SOUND 8
		EXTERN 68
		REDRAW
		SET FLAG $PROCESOR3 = 1
		SET TILE (7, 4)= 0
		SHOW
	END	
	
END



PRESS_FIRE AT SCREEN 16 
	IF $SCHOOL <> 1 #COMPRUEBA SI YA HEMOS PASADO POR AQUI Y MATADO AL JEFE
	THEN
		BREAK #SALIMOS
	END
	
	IF PLAYER_TOUCHES 13, 7
	THEN
		ENEMY 0 OFF
		DECORATIONS #SE MUEVE PALANCA, ABRE EL SUELO Y CAE EL JEFE
			13, 7, 18
			8, 6, 0
			9, 6, 0
			8, 7, 30
			9, 7, 31
			8, 8, 46
			9, 8, 47
		END
		REDRAW
		EXTERN 15 #habla el jefe
		REDRAW
		DECORATIONS # DESAPARACE JEFE, PONEMOS LAVA Y PLATAFORMA ARRIBA PARA SALIR.
			8, 7, 0
			9, 7, 0
			8, 8, 14
			9, 8, 14
			9, 3, 4
			14, 1, 0
			14, 2, 0
		END
		REDRAW
		MUSIC 7
		EXTERN 16 #consigues la llave shinobi
		REDRAW
		SET $SCHOOL = 2 #BOSS MUERTO Y LLAVE SHINOBI LOGRADA
	END	
END

## PUERTA DE SALIDA 2
PRESS_FIRE AT SCREEN 19
	IF PLAYER_TOUCHES 12, 8
	THEN
		SET FLAG $PUERTA = 22
		MUSIC 5
		PAUSE 50
		WARP_TO_LEVEL 0, 0, 2, 5, 1
	END	
END


ENTERING SCREEN 16 #BOSS

	
	IF $SCHOOL <> 1
	IF $PANTPRO <> 17
	THEN
		MUSIC 7
	END

	IF $SCHOOL <> 1 #COMPRUEBA SI YA HEMOS PASADO POR AQUI Y MATADO AL JEFE
	THEN
		ENEMY 0 OFF
		DECORATIONS #TODO APAGADO SI YA HEMOS MATADO AL JEFE
			13, 7, 18
			9, 3, 4
			8, 6, 0
			9, 6, 0
			8, 7, 0
			9, 7, 0
			8, 8, 14
			9, 8, 14
			14, 1, 0
			14, 2, 0
		END
		BREAK #SALIMOS
	END	
	
	IF $SCHOOL = 1
	THEN
		MUSIC 6
	END

	
	
END


END_OF_LEVEL


