##############################################################################
# LEVEL 3 CASTLEVANIA
##############################################################################
INC_DECORATIONS decorations3.spt

ENTERING GAME
	
	IF $BOLAS > 3
	THEN
		SET $DISPARO = 1#ACTIVAMOS DISPARO SI TIENE LA BOLA 4 (AGUA BENDITA)
		MUSIC 10
	END
	
	IF $PUERTA = 31 #VENIMOS DE ROOM BELMONT 
	THEN
		SET $PUERTA = 0
		WARP_TO 15, 4, 8
	END
	
	IF $PUERTA = 32 #VENIMOS DE ROOM PALANCAS 
	THEN
		SET $PUERTA = 0
		WARP_TO 18, 5, 8
	END

	IF $PUERTA = 36 #VENIMOS DE ROOM SECRETA 
	THEN
		SET $PUERTA = 0
		MUSIC 10
		WARP_TO 1, 3, 8
	END
	
	IF TRUE
	THEN
	SET $PUERTA = 0
	##SET $NIVELPRO = 3
	##WARP_TO 1, 14, 6 ## BORRAR DESPUES DE HACER JEFE
	END

END

ENTERING ANY
	IF TRUE
	THEN
		SET $PUERTA = 0
		SET $XTORCH1 = 99 ## 99 DEJA DE DIBUJAR LAS ANTORCHAS
		SET $YTORCH1 = 99
		SET $XTORCH2 = 99
		SET $YTORCH2 = 99
		SET $BOSSLIFE = 0
		TIMER_STOP
	END
END

ENTERING SCREEN 1
	IF TRUE
	THEN
		PRINT_TILE_AT (5, 0) = 11
		PRINT_TILE_AT (19, 14) = 11
	END
END

ENTERING SCREEN 5
	IF TRUE
	THEN
		SET_TIMER 1, 2 ## SET TIMER FOR FIRE
		TIMER_START
		SET $VUELTA = 0 ## SE USA AQUI PARA ANIMACION DEL FUEGO
		SET $XTORCH1 = 12 #COORDENADAS PRIMERA ANTORCHA
		SET $YTORCH1 = 7
		SET $XTORCH2 = 12 #COORDENADAS SEGUNDA ANTORCHA (PONIENDO LA MISMA SOLO HAY UNA)
		SET $YTORCH2 = 7
	END
END

ENTERING SCREEN 6
	IF TRUE
	THEN
		SET_TIMER 1, 3 ## SET TIMER FOR FIRE
		TIMER_START
		SET $VUELTA = 0 ## SE USA AQUI PARA ANIMACION DEL FUEGO
		SET $XTORCH1 = 4 #COORDENADAS PRIMERA ANTORCHA
		SET $YTORCH1 = 7
		SET $XTORCH2 = 10 #COORDENADAS SEGUNDA ANTORCHA (99 ES QUE NO EXISTE)
		SET $YTORCH2 = 7
	END
	
	## PROCESOR 4 
	IF $SCHOOL > 26
	IF $PROCESOR4 < 1
	THEN
		SET TILE (6,8)= 34
		SHOW
	END	
	
END

ENTERING SCREEN 7
	IF TRUE
	THEN
		SET_TIMER 1, 2 ## SET TIMER FOR FIRE
		TIMER_START
		SET $VUELTA = 0 ## SE USA AQUI PARA ANIMACION DEL FUEGO
		SET $XTORCH1 = 4 #COORDENADAS PRIMERA ANTORCHA
		SET $YTORCH1 = 7
		SET $XTORCH2 = 4 #COORDENADAS SEGUNDA ANTORCHA (PONIENDO LA MISMA SOLO HAY UNA)
		SET $YTORCH2 = 7
	END
END

ENTERING SCREEN 10
	IF TRUE
	THEN
		SET_TIMER 1, 2 ## SET TIMER FOR FIRE
		TIMER_START
		SET $VUELTA = 0 ## SE USA AQUI PARA ANIMACION DEL FUEGO
		SET $XTORCH1 = 8 #COORDENADAS PRIMERA ANTORCHA
		SET $YTORCH1 = 7
		SET $XTORCH2 = 8 #COORDENADAS SEGUNDA ANTORCHA (PONIENDO LA MISMA SOLO HAY UNA)
		SET $YTORCH2 = 7
	END
	
	IF $BOLAS < 4
	THEN
		ENEMY 0 OFF
	END
	
END

ENTERING SCREEN 11
	IF TRUE
	THEN
		SET_TIMER 1, 3 ## SET TIMER FOR FIRE
		TIMER_START
		SET $VUELTA = 0 ## SE USA AQUI PARA ANIMACION DEL FUEGO
		SET $XTORCH1 = 4 #COORDENADAS PRIMERA ANTORCHA
		SET $YTORCH1 = 7
		SET $XTORCH2 = 11 #COORDENADAS SEGUNDA ANTORCHA
		SET $YTORCH2 = 7
	END
	
	IF $BOLAS < 4
	THEN
		ENEMY 0 OFF
		ENEMY 1 OFF
	END	
	
END

ENTERING SCREEN 12
	IF TRUE
	THEN
		SET_TIMER 1, 3 ## SET TIMER FOR FIRE
		TIMER_START
		SET $VUELTA = 0 ## SE USA AQUI PARA ANIMACION DEL FUEGO
		SET $XTORCH1 = 7 #COORDENADAS PRIMERA ANTORCHA
		SET $YTORCH1 = 7
		SET $XTORCH2 = 7 #COORDENADAS SEGUNDA ANTORCHA (PONIENDO LA MISMA SOLO HAY UNA)
		SET $YTORCH2 = 7
	END

	IF $BOLAS < 4
	THEN
		ENEMY 1 OFF
	END	
	
END

ENTERING SCREEN 15
	IF TRUE
	THEN
		SET_TIMER 1, 3 ## SET TIMER FOR FIRE
		TIMER_START
		SET $VUELTA = 0 ## SE USA AQUI PARA ANIMACION DEL FUEGO
		SET $XTORCH1 = 8 #COORDENADAS PRIMERA ANTORCHA
		SET $YTORCH1 = 7
		SET $XTORCH2 = 8 #COORDENADAS SEGUNDA ANTORCHA (PONIENDO LA MISMA SOLO HAY UNA)
		SET $YTORCH2 = 7
	END
END

ENTERING SCREEN 19
	IF TRUE
	THEN
		SET_TIMER 1, 3 ## SET TIMER FOR FIRE
		TIMER_START
		SET $VUELTA = 0 ## SE USA AQUI PARA ANIMACION DEL FUEGO
		SET $XTORCH1 = 5 #COORDENADAS PRIMERA ANTORCHA
		SET $YTORCH1 = 7
		SET $XTORCH2 = 5 #COORDENADAS SEGUNDA ANTORCHA (PONIENDO LA MISMA SOLO HAY UNA)
		SET $YTORCH2 = 7
	END
END


PRESS_FIRE AT ANY
	
	IF $BOLAS > 2 ## SI TENEMOS LA BOLA 3 PODEMOS USAR EL VIENTO PARA APAGAR LAS ANTORCHAS.
	IF PLAYER_IN_Y 104, 120
	IF NPANT_NOT 0
	THEN
		SET $VUELTA = 5 ## APAGA LAS ANTORCHAS DE LA PANTALLA
	END
	
END

PRESS_FIRE AT SCREEN 1

	IF $SCHOOL < 14 ## SI NO HEMOS DERROTADO AL BOSS
	IF PLAYER_TOUCHES 3, 2 ##  PUERTA HACIA BOSS
	THEN
			MUSIC 5
			PAUSE 60
			MUSIC 6
			WARP_TO 0, 13, 8
	END
	
	IF PLAYER_TOUCHES 3, 2 #SI YA HEMOS DERROTADO AL BOSS,NOS LLEVA AL PUEBLO
	THEN
		SET FLAG $PUERTA = 22 #MISMA PUERTA QUE SALIENDO DEL NIVEL MARIO, NOS SIRVE
		MUSIC 5
		PAUSE 50
		WARP_TO_LEVEL 0, 1, 2, 5, 1
	END	
	
	IF PLAYER_TOUCHES 3, 8 ##  PUERTA SECRETA ABAJO
	THEN
			SET FLAG $PUERTA = 36
			MUSIC 5
			PAUSE 60
			WARP_TO_LEVEL 6, 1, 2, 5, 1
	END

END

PRESS_FIRE AT SCREEN 6

	IF $SCHOOL > 26
	IF $PROCESOR4 < 1
	IF PLAYER_TOUCHES 6,8
	THEN
		SOUND 8
		EXTERN 68
		REDRAW
		SET FLAG $PROCESOR4 = 1
		SET TILE (6,8)= 0
		SHOW
	END	
END	


PRESS_FIRE AT SCREEN 9

	IF PLAYER_TOUCHES 13, 8 ##  PUERTA HACIA PLANTA BAJA
	THEN
		MUSIC 5
		PAUSE 60
		MUSIC 4
		WARP_TO 19, 13, 2
	END

END

PRESS_FIRE AT SCREEN 10

	IF PLAYER_TOUCHES 1, 8 ## PUERTA DE SALIDA 1
	THEN
		SET FLAG $PUERTA = 30
		WARP_TO_LEVEL 0, 0, 2, 5, 1
	END	
	
END

PRESS_FIRE AT SCREEN 15

	IF PLAYER_TOUCHES 4, 8 ## PUERTA PACO BELMONT
	THEN
		SET FLAG $PUERTA = 31
		MUSIC 5
		PAUSE 50
		WARP_TO_LEVEL 6, 0, 2, 5, 1
	END	
END


PRESS_FIRE AT SCREEN 18

	IF PLAYER_TOUCHES 5, 8 ##  PUERTA PALANCAS
	THEN
			SET $BOSSLIFE = 0 ## INICIALIZAMOS FLAGS PARALA PRUEBA DE LAS PALANCAS
			SET $PALANCA1 = 0
			SET $PALANCA2 = 0
			SET $PALANCA3 = 0
			SET $PALANCA4 = 0
			SET FLAG $PUERTA = 32
			MUSIC 5
			PAUSE 50
			WARP_TO_LEVEL 6, 1, 2, 5, 1
	END

END

PRESS_FIRE AT SCREEN 19

	IF PLAYER_TOUCHES 13, 2 ##  PUERTA HACIA PLANTA ALTA
	THEN
			MUSIC 5
			PAUSE 60
			MUSIC 10
			WARP_TO 9, 13, 8
	END
END

##### BOSS #####

ENTERING SCREEN 0
	IF TRUE
	THEN
		MUSIC 6
		SET_TIMER 1, 1 ## SET TIMER FOR BOSS
		TIMER_START
		SET FLAG $BOSSLIFE = 12
		SET $VUELTA = 50
		SET FLAG $XBALL = 3 ## INICIAMOS COORDENADAS DEL MURCIELAGO
		SET FLAG $YBALL = 3	
		SET $SUBEBAJA = 9 ## 9 PARA QUE NO EMPIECE A SUBIR HASTA LA VUELTA 11
	END
	
END

## PUERTA DESPUES DE MATAR AL BOSS
PRESS_FIRE AT SCREEN 0

	IF $SCHOOL = 14 ## BOSS MUERTO
	IF PLAYER_TOUCHES 1, 8
	#SI HEMOS MATADO AL BOSS Y ESTAMOS DELANTE DE LA PUERTA = SE ABRE
	THEN
		SET FLAG $PUERTA = 22 #MISMA PUERTA QUE SALIENDO DEL NIVEL MARIO, NOS SIRVE
		MUSIC 5
		PAUSE 50
		WARP_TO_LEVEL 0, 1, 2, 5, 1
	END	
END

PLAYER_KILLS_ENEMY  ## CODIGO DEL BOSS FINAL

	IF NPANT_NOT 0
	THEN
		BREAK
	END

	IF TRUE
	THEN
		PRINT_TILE_AT (#$XBARRABOSS, 0) = 33
		DEC $XBARRABOSS, 1
		DEC FLAG $BOSSLIFE, 1
	END
	
	IF $BOSSLIFE > 0
	THEN
		EXTERN 252 ## RESUCITA EL ENEMIGO 0 QUE ES EL PUNTO DEBIL DEL BOSS.
	END
	
	IF $BOSSLIFE = 0
	THEN
		MUSIC 4
		TIMER_STOP
		
		SET TILE (#$XBALL, #$YBALL) = 14
		SET TILE (1,3) = 20
		SET TILE (1,4) = 20
		SET TILE (2,3) = 20
		SET TILE (2,4) = 20
		SHOW
		SOUND 6
		PAUSE 10
		SET TILE (1,3) = 21
		SET TILE (1,4) = 21
		SET TILE (2,3) = 21
		SET TILE (2,4) = 21
		SHOW
		SOUND 6
		PAUSE 10
		SET TILE (1,3) = 20
		SET TILE (1,4) = 20
		SET TILE (2,3) = 20
		SET TILE (2,4) = 20
		SHOW
		SOUND 6
		PAUSE 10
		SET TILE (1,3) = 23
		SET TILE (1,4) = 23
		SET TILE (2,3) = 23
		SET TILE (2,4) = 23
		SHOW
		SOUND 6
		PAUSE 10
		SET TILE (1,3) = 14
		SET TILE (1,4) = 14
		SET TILE (2,3) = 14
		SET TILE (2,4) = 14
		SET TILE (1,8) = 22 #PUERTA PARA SALIR
		SHOW
		EXTERN 40 # COGEMOS LA CRUZ DEL PENITENTE
		REDRAW
		SET FLAG $SCHOOL = 14

	END
	
END


### TIMERS

ON_TIMER_OFF

	## ANIMACION ANTORCHAS

	IF $XTORCH1 <> 99
	IF $VUELTA = 0
	THEN
		SET TILE (#$XTORCH1, #$YTORCH1)= 21
		SET TILE (#$XTORCH2, #$YTORCH2)= 21
		SET $VUELTA = 1
		SHOW
		SET_TIMER 1, 3	
		BREAK		
	END
	
	IF $XTORCH1 <> 99
	IF $VUELTA = 1
	THEN
		SET TILE (#$XTORCH1, #$YTORCH1)= 20
		SET TILE (#$XTORCH2, #$YTORCH2)= 20
		SET $VUELTA = 0
		SHOW
		SET_TIMER 1, 3	
		BREAK
	END
	
	IF $XTORCH1 <> 99
	IF $VUELTA = 5
	THEN
		SET TILE (#$XTORCH1, #$YTORCH1)= 23
		SET TILE (#$XTORCH2, #$YTORCH2)= 23
		SHOW
		SOUND 6
		PAUSE 10
		TIMER_OFF
		SET TILE (#$XTORCH1, #$YTORCH1)= 0
		SET TILE (#$XTORCH2, #$YTORCH2)= 0
		SET $XTORCH1 = 99
		BREAK
	END
	
### ANIMACION PARA BOSS

	IF NPANT_NOT 0
	THEN
		BREAK
	END
	
	IF $BOSSLIFE > 0
	THEN
		INC $VUELTA, 1 
		SET_TIMER 1, 1
	END
	
	IF $VUELTA = 51
	THEN
		EXTERN 38 # NOS HABLA EL BOSS
		REDRAW
		## IMPRIMIR BARRA DE VIDA DEL BOSS
		SET $XBARRABOSS = 21
		PRINT_TILE_AT (10, 0) = 32
		PRINT_TILE_AT (12, 0) = 32
		PRINT_TILE_AT (14, 0) = 32
		PRINT_TILE_AT (16, 0) = 32
		PRINT_TILE_AT (18, 0) = 32
		PRINT_TILE_AT (20, 0) = 32
		SHOW
	END
	
	IF $VUELTA = 60
	THEN
		SET TILE (1,3)=30
		SET TILE (1,4)=31
		SET TILE (#$XBALL, #$YBALL)=19 #LANZA BATARANG
		SHOW
		SOUND 6
		
	END	

	IF $XBALL = 13
	THEN 
		SET TILE (#$XBALL, #$YBALL)=14 #BORRAMOS DISPARO BOSS AL LLEGAR AL FINAL DE LA PANTALLA
		SET $XBALL = 3
		SET $YBALL = 3
		SET $VUELTA = 51
		SET $SUBEBAJA = 9
		SHOW
	END
	
	IF $YBALL = 3
	THEN
		SET $SUBEBAJA = 0
	END
	
	IF $YBALL = 5
	THEN
		SET $SUBEBAJA = 1
	END
	
	IF $VUELTA = 65
	THEN
		SET TILE (1,3)=26
		SET TILE (1,4)=28
	END	
	
	IF $VUELTA = 61
	THEN
		SET $SUBEBAJA = 1
	END
	
	
	IF $VUELTA > 61
	THEN
		SET TILE (#$XBALL, #$YBALL)=14 #BORRAMOS DISPARO BOSS
	END
	
	IF $SUBEBAJA = 0
	THEN
	 INC $YBALL, 1
	END
	
	IF $SUBEBAJA = 1
	THEN
	 DEC $YBALL, 1
	END

	
	IF $VUELTA > 61
	THEN
		INC $XBALL, 1
		SET TILE (#$XBALL, #$YBALL)=19 #PINTAMOS DISPARO BOSS
		SHOW
	END
	
END



END_OF_LEVEL


