##############################################################################
# LEVEL 1 SONIC GREEN HILL
##############################################################################
INC_DECORATIONS decorations1.spt

ENTERING GAME
		IF TRUE
		THEN
		SET $PUERTA = 0
		##SET $NIVELPRO = 1
		END
		
		IF $BOLAS > 0
		THEN
		SET $DISPARO = 1#ACTIVAMOS DISPARO SI TIENE LA BOLA 1 (FUEGO)
		#WARP_TO 19,1,5 ## BORRAR DESPUES DE HACER JEFE
		END
END

ENTERING ANY
	IF TRUE
	THEN
	SET $PUERTA = 0
	END
END

ENTERING SCREEN 19

	IF TRUE
	THEN
		MUSIC 6
	END
		
END
		
## PUERTAS GREEN HILL

## PUERTA DE SALIDA 1
PRESS_FIRE AT SCREEN 0
	IF PLAYER_TOUCHES 1, 7
	THEN
		SET FLAG $PUERTA = 11
		WARP_TO_LEVEL 0, 0, 2, 5, 1
		##WARP_TO 18, 1, 1 #ACTIVAR PARA IR A LA ULTIMA PANTALLA
	END	
END

## TOTEM ALTO COMBINACION PALANCAS
PRESS_FIRE AT SCREEN 11
	IF PLAYER_TOUCHES 6, 7
	THEN
		EXTERN 28 #TE DA LA COMBINACION

		REDRAW
	END	
END

## PUERTA BOSS
PRESS_FIRE AT SCREEN 18


	IF $SCHOOL < 6 ## NO HEMOS DERROTADO AL BOSS
	IF PLAYER_TOUCHES 10, 6 ## PERSONAJE DELANTE DE PUERTA FINAL
	#SI EL BOSS NO HA SIDO DERROTADO Y ESTAMOS DELANTE DE LA PUERTA = VAMOS A LA PANTALLA DEL BOSS (19)
	THEN
		MUSIC 5
		PAUSE 50
		WARP_TO 19, 1, 7
	END 

	IF PLAYER_TOUCHES 10, 6 #SI YA HEMOS DERROTADO AL BOSS,NOS LLEVA AL PUEBLO
	THEN
		SET FLAG $PUERTA = 22 #MISMA PUERTA QUE SALIENDO DEL NIVEL MARIO, NOS SIRVE
		MUSIC 5
		PAUSE 50
		WARP_TO_LEVEL 0, 1, 2, 5, 1
	END	
END

## PUERTA DESPUES DE MATAR AL BOSS
PRESS_FIRE AT SCREEN 19

	IF $SCHOOL = 6 ## BOSS MUERTO
	IF PLAYER_TOUCHES 2, 5
	#SI HEMOS MATADO AL BOSS Y ESTAMOS DELANTE DE LA PUERTA = SE ABRE
	THEN
		SET FLAG $PUERTA = 22 #MISMA PUERTA QUE SALIENDO DEL NIVEL MARIO, NOS SIRVE
		MUSIC 5
		PAUSE 50
		WARP_TO_LEVEL 0, 1, 2, 5, 1
	END	
END




ENTERING SCREEN 19
	IF TRUE
	THEN
		MUSIC 6
		SET_TIMER 1, 1 ## SET TIMER FOR BOSS
		TIMER_START
		SET FLAG $BOSSLIFE = 5
		SET FLAG $XBALL = 10 ## INICIAMOS COORDENADAS DE LA BOLA
		SET FLAG $YBALL = 2
		SET $XINC = 1
		SET $YINC = 0
		SET TILE (#$XBALL, #$YBALL) = 35
		## IMPRIMIR BARRA DE VIDA DEL BOSS
		SET $XBARRABOSS = 16
		PRINT_TILE_AT (12, 2) = 40
		PRINT_TILE_AT (14, 2) = 40
		PRINT_TILE_AT (16, 2) = 40
		PRINT_TILE_AT (17, 2) = 7		
		
	END
END

PLAYER_KILLS_ENEMY  ## CODIGO DEL BOSS FINAL

	IF NPANT_NOT 19
	THEN
		BREAK
	END

	IF TRUE
	THEN
		PRINT_TILE_AT (#$XBARRABOSS, 2) = 7
		DEC $XBARRABOSS, 1
		DEC FLAG $BOSSLIFE, 1
	END
	
	IF $BOSSLIFE > 0
	THEN
		EXTERN 252 ## RESUCITA EL ENEMIGO 0 QUE ES EL PUNTO DEBIL DEL BOSS.
	END
	
	IF $BOSSLIFE = 0
	THEN
		MUSIC 2
		TIMER_STOP
		
		SET TILE (#$XBALL, #$YBALL) = 7
		SET TILE (10,3) = 36
		SHOW
		SOUND 6
		PAUSE 8
		SET TILE (10,3) = 7
		SET TILE (11,3) = 36
		SET TILE (12,3) = 7
		SHOW
		SOUND 6
		PAUSE 8
		SET TILE (11,3) = 7
		SET TILE (10,4) = 36
		SHOW
		SOUND 6
		PAUSE 8
		SET TILE (10,4) = 7
		SET TILE (11,4) = 36
		SET TILE (12,4) = 7
		SHOW
		SOUND 6
		PAUSE 8	
		SET TILE (11,4) = 7
		SET TILE (2,5) = 22
		SHOW
		
		EXTERN 25#COGEMOS LA LLAVE DE LA TORRE
		REDRAW
		SET FLAG $SCHOOL = 6
		
	END
	
END

## BOSS TIMER
ON_TIMER_OFF

	IF $BOSSLIFE > 0
	THEN
		SET_TIMER 1, 1
		## BORRAMOS LA BOLA
		SET TILE (#$XBALL, #$YBALL) = 7
		SET $VUELTA = 0
	END
	
	## COMPROBAMOS SI LLEGA A LOS LIMITES
	IF $XBALL = 13
	THEN
		SET $XINC = 0
		SET $YINC = 1
	END
	
	IF $YBALL = 5
	THEN
		SET $XINC = 2
		SET $YINC = 0
	END

	IF $XBALL = 9
	THEN
		SET $XINC = 0
		SET $YINC = 2
		INC FLAG $VUELTA, 1
	END

	IF $YBALL = 2
	THEN
		INC FLAG $VUELTA, 1
	END
	
	IF $VUELTA = 2
	THEN
		SET $XINC = 1
		SET $YINC = 0
	END	
	
	## SUMAMOS O RESTAMOS COORDENADAS A LA BOLA
	IF $XINC = 1
	THEN
		INC FLAG $XBALL, 1
	END

	IF $XINC = 2
	THEN
		DEC FLAG $XBALL, 1
	END

	IF $YINC = 1
	THEN
		INC FLAG $YBALL, 1
	END	

	IF $YINC = 2
	THEN
		DEC FLAG $YBALL, 1
	END	
	
	IF $BOSSLIFE > 0
	THEN
		## PINTA LA BOLA EN SU NUEVA POSICION
		SET TILE (#$XBALL, #$YBALL) = 35
		SHOW
	END
	
END


END_OF_LEVEL
