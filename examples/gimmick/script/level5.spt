##############################################################################
# LEVEL 5 SHINOBI
##############################################################################
INC_DECORATIONS decorations5.spt

ENTERING GAME

	
	IF $BOLAS > 1
	THEN
	SET $DISPARO = 1#ACTIVAMOS DISPARO SI TIENE LA BOLA 2 (PIEDRA)
	END
	
	IF $PUERTA = 52
	THEN
	SET $PUERTA = 0
	WARP_TO 5, 11, 4	
	END	
	
	IF TRUE
	THEN
		##SET $NIVELPRO = 5
		SET $PUERTA = 0
		##WARP_TO 16, 0, 3 ## BORRAR DESPUES DE HACER JEFE
	END


	
END

ENTERING ANY
	IF TRUE
	THEN
	SET $PUERTA = 0
	END
END

PRESS_FIRE AT SCREEN 0

	
	IF PLAYER_TOUCHES 1, 8 ## PUERTA DE SALIDA 1
	THEN
		SET FLAG $PUERTA = 51
		WARP_TO_LEVEL 0, 0, 2, 5, 1
		##WARP_TO 18,10,8
		
	END	
END

PRESS_FIRE AT SCREEN 5

	IF PLAYER_TOUCHES 11, 4 ## PUERTA CALVA MAGICA
	THEN
		SET FLAG $PUERTA = 52
		WARP_TO_LEVEL 6, 0, 2, 5, 1
	END	
END

PRESS_FIRE AT SCREEN 8

	IF PLAYER_TOUCHES 14, 8 ## PUERTA HACIA FASE 5-2 (AGUA)
	THEN
		MUSIC 5
		PAUSE 50
		MUSIC 3
		WARP_TO 10, 0, 4
	END	
END

PRESS_FIRE AT SCREEN 10

	IF PLAYER_TOUCHES 0, 4 ## PUERTA HACIA FASE 5-1 (CHINA TOWN)
	THEN
		MUSIC 5
		PAUSE 50
		MUSIC 9
		WARP_TO 8, 14, 8
	END	
END

## PUERTA BOSS
PRESS_FIRE AT SCREEN 18

	IF $SCHOOL < 10 ## SI NO HEMOS DERROTADO AL BOSS TODAVIA
	IF PLAYER_TOUCHES 12, 8 ## PERSONAJE DELANTE DE PUERTA FINAL
	#SI EL BOSS NO HA SIDO DERROTADO Y ESTAMOS DELANTE DE LA PUERTA = VAMOS A LA PANTALLA DEL BOSS (19)
	THEN
		SET FLAG $PUERTA = 55
		MUSIC 5
		PAUSE 50
		WARP_TO 19, 1, 8
	END 

	IF PLAYER_TOUCHES 12, 8 #SI YA HEMOS DERROTADO AL BOSS,NOS LLEVA AL PUEBLO
	THEN
		SET FLAG $PUERTA = 22 #MISMA PUERTA QUE SALIENDO DEL NIVEL MARIO, NOS SIRVE
		MUSIC 5
		PAUSE 50
		WARP_TO_LEVEL 0, 1, 2, 5, 1
	END	
END


## PUERTA DESPUES DE MATAR AL BOSS
PRESS_FIRE AT SCREEN 19

	IF $SCHOOL = 10 ## BOSS MUERTO
	IF PLAYER_TOUCHES 1, 8
	#SI HEMOS MATADO AL BOSS Y ESTAMOS DELANTE DE LA PUERTA = SE ABRE
	THEN
		SET FLAG $PUERTA = 22 #MISMA PUERTA QUE SALIENDO DEL NIVEL MARIO, NOS SIRVE
		MUSIC 5
		PAUSE 50
		WARP_TO_LEVEL 0, 1, 2, 5, 1
	END	
END

##### BOSS #####

ENTERING SCREEN 19
	IF TRUE
	THEN
		MUSIC 6
		SET_TIMER 1, 1 ## SET TIMER FOR BOSS
		TIMER_START
		SET FLAG $BOSSLIFE = 8
		SET $VUELTA = 0
		SET FLAG $XBALL = 10 ## INICIAMOS COORDENADAS DE LA BOLA
		SET FLAG $YBALL = 8		
		
	END
	
END

PLAYER_KILLS_ENEMY  ## CODIGO DEL BOSS FINAL

	IF NPANT_NOT 19
	THEN
		BREAK
	END

	IF TRUE
	THEN
		PRINT_TILE_AT (#$XBARRABOSS, 0) = 11
		DEC $XBARRABOSS, 1
		DEC FLAG $BOSSLIFE, 1
	END
	
	IF $BOSSLIFE > 0
	THEN
		EXTERN 252 ## RESUCITA EL ENEMIGO 0 QUE ES EL PUNTO DEBIL DEL BOSS.
	END
	
	IF $BOSSLIFE = 0
	THEN
		MUSIC 9
		TIMER_STOP
		
		SET TILE (#$XBALL, #$YBALL) = 11
		SET TILE (10,8) = 11
		SET TILE (11,7) = 38
		SET TILE (11,8) = 38
		SHOW
		SOUND 6
		PAUSE 8
		SOUND 6
		PAUSE 8
		SOUND 6
		PAUSE 8
		SOUND 6
		PAUSE 8
		SOUND 6
		PAUSE 8
		SOUND 6
		PAUSE 8
		SET TILE (11,7) = 11
		SET TILE (11,8) = 11
		SET TILE (1,8) = 32
		SHOW
		EXTERN 32#COGEMOS LA BOLA DE HOLY WATER O AJO, YA VEREMOS...
		REDRAW
		SET FLAG $SCHOOL = 10

	END
	
END


## BOSS TIMER
ON_TIMER_OFF

	IF $BOSSLIFE > 0
	THEN
		INC $VUELTA, 1 
		SET_TIMER 1, 1
	END
	
	IF $VUELTA = 1
	THEN
		EXTERN 31 # NOS HABLA EL BOSS
		REDRAW
		SET $XBARRABOSS = 19
		PRINT_TILE_AT (12, 0) = 40
		PRINT_TILE_AT (14, 0) = 40
		PRINT_TILE_AT (16, 0) = 40
		PRINT_TILE_AT (18, 0) = 40
		SHOW
	END
	
	IF $VUELTA = 10
	THEN
		SET TILE (10,8)=35
		SET TILE (11,8)=36
		SHOW
		SOUND 7
	END	

	IF $XBALL = 1
	THEN 
		SET TILE (#$XBALL, #$YBALL)=11 #BORRAMOS ONDA VITAL/HADOUKEN
		SET $XBALL = 10
		SET $VUELTA = 1
		DEC $YBALL, 1
		SHOW
	END
	
	IF $YBALL = 6
	THEN
		SET $YBALL = 8
	END
	
	IF $VUELTA = 15
	THEN
		DEC $XBALL, 1
		SET TILE (10,8)=11
		SET TILE (11,8)=34
		SET TILE (#$XBALL, #$YBALL)=17 #ONDA VITAL/HADOUKEN
		SHOW
		SOUND 6
		
	END	
	
	IF $VUELTA > 15
	THEN
		SET TILE (#$XBALL, #$YBALL)=11 #BORRAMOS ONDA VITAL/HADOUKEN
		DEC $XBALL, 1
		SET TILE (#$XBALL, #$YBALL)=17 #ONDA VITAL/HADOUKEN
		SHOW
	END
	

END


END_OF_LEVEL

